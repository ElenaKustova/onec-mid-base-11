#Область ПрограммныйИнтерфейс

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
// Дополнить форму.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма, в которой выполняется команда.
Процедура ДополнитьФорму(Форма)Экспорт  
	
	ИмяФормы = Форма.ИмяФормы;
	Если ИмяФормы = "Документ.ЗаказПокупателя.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма); 
		ДобавитьПолеСогласованнаяСкидкаВГруппаСкидка(Форма); 
		ДобавитьКомандуПересчитатьТаблицуВГруппаСкидка(Форма);
	ИначеЕсли ИмяФормы = "Документ.ПоступлениеТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);    
	ИначеЕсли ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
	ИначеЕсли ИмяФормы = "Документ.ОплатаОтПокупателя.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма); 
	ИначеЕсли ИмяФормы = "Документ.ОплатаПоставщику.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма);
	КонецЕсли;
	
КонецПроцедуры 
#КонецЕсли

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции  

Процедура ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма)
	
	ПолеВвода = Форма.Элементы.Добавить("КонтактноеЛицо", Тип("ПолеФормы"),Форма.Элементы.ГруппаШапкаПраво);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.ДП_КонтактноеЛицо";
	
КонецПроцедуры   

Процедура ДобавитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма)
	
	ПолеВвода = Форма.Элементы.Вставить("КонтактноеЛицо", Тип("ПолеФормы"), ,Форма.Элементы.СуммаДокумента);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.ДП_КонтактноеЛицо";
	
КонецПроцедуры  

Процедура ДобавитьПолеСогласованнаяСкидкаВГруппаСкидка(Форма)
	
	Группа = Форма.Элементы.Добавить("ГруппаСкидка",Тип("ГруппаФормы"), Форма.Элементы.ГруппаШапкаЛево);
	Группа.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	Группа.ОтображатьЗаголовок = ЛОЖЬ;
	Группа.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	
	ПолеВвода = Форма.Элементы.Добавить("СогласованнаяСкидка", Тип("ПолеФормы"),Форма.Элементы.ГруппаСкидка);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.ДП_СогласованнаяСкидка";
	ПолеВвода.УстановитьДействие("ПриИзменении", "ДП_СогласованнаяСкидкаПриИзменении");
КонецПроцедуры

Процедура ДобавитьКомандуПересчитатьТаблицуВГруппаСкидка(Форма)
	
	Если Форма.Элементы.Найти("КнопкаПересчитатьТаблицу") = Неопределено Тогда
		КомандаПересчитатьТаблицу = Форма.Команды.Добавить("ПересчитатьТаблицу"); 
		КомандаПересчитатьТаблицу.Заголовок = "Пересчитать таблицу";
		КомандаПересчитатьТаблицу.Действие = "КомандаПересчитатьТаблицу";
		КнопкаКоманды = Форма.Элементы.Добавить("КнопкаПересчитатьТаблицу", Тип("КнопкаФормы"),Форма.Элементы.ГруппаСкидка);
		КнопкаКоманды.ИмяКоманды = "ПересчитатьТаблицу";
		КнопкаКоманды.Вид = ВидКнопкиФормы.ОбычнаяКнопка;
	КонецЕсли;
	
КонецПроцедуры
#КонецОбласти