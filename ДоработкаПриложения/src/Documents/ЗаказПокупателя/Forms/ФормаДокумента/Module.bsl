
#Область ОбработчикиСобытийЭлементовШапкиФормы
&НаКлиенте
Процедура ДП_ДП_СогласованнаяСкидкаПриИзмененииПосле(Элемент)
	
	Если Объект.Товары.Количество() = 0 И Объект.Услуги.Количество() = 0 Тогда
		Возврат; 
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ДП_ПослеОтветаНаВопрос", ЭтотОбъект);
	ПоказатьВопрос(Оповещение, "Изменился процент скидки. Пересчитать табличную часть?", РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции
&ИзменениеИКонтроль("РассчитатьСуммуСтроки")
&НаКлиенте
Процедура ДП_РассчитатьСуммуСтроки(ТекущиеДанные)
    #Удаление
	КоэффициентСкидки = 1 - ТекущиеДанные.Скидка / 100;
	ТекущиеДанные.Сумма = ТекущиеДанные.Цена * ТекущиеДанные.Количество * КоэффициентСкидки;
    #КонецУдаления

	#Вставка 
	ДП_ПересчитатьКолонкуСумма(ТекущиеДанные);
	#КонецВставки
	
КонецПроцедуры
&НаКлиенте
Процедура ДП_ПересчитатьТаблицуПосле(Команда)
	
	Если ДП_НетДанныхВТаблицах()Или НЕ ЗначениеЗаполнено(Объект.ДП_СогласованнаяСкидка) Тогда
		Возврат; 
	КонецЕсли;
	
	ДП_РассчитатьСуммуСтрок(); 
КонецПроцедуры


&НаКлиенте 
Процедура ДП_ПослеОтветаНаВопрос(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	ДП_РассчитатьСуммуСтрок();      
	
КонецПроцедуры

&НаКлиенте
Функция ДП_НетДанныхВТаблицах()
	
	Возврат Объект.Товары.Количество() = 0 И Объект.Услуги.Количество()= 0;
	
КонецФункции 

&НаКлиенте
Процедура ДП_РассчитатьСуммуСтрок() 
	
	Для Каждого Строка Из Объект.Товары Цикл
		ДП_ПересчитатьКолонкуСумма(Строка);
	КонецЦикла;   
	
	Для Каждого Строка Из Объект.Услуги Цикл
		ДП_ПересчитатьКолонкуСумма(Строка);
	КонецЦикла; 
	
	РассчитатьСуммуДокумента();
	
КонецПроцедуры 

&НаКлиенте
Процедура ДП_ПересчитатьКолонкуСумма(Строка)
	
	КоэффициентСкидки = 1 - (Строка.Скидка + Объект.ДП_СогласованнаяСкидка)/ 100;
	
	Если КоэффициентСкидки > 0 Тогда
	    Строка.Сумма = Строка.Цена * Строка.Количество* КоэффициентСкидки;
	Иначе
		Строка.Сумма = 0;
	КонецЕсли;
	
КонецПроцедуры
#КонецОбласти
